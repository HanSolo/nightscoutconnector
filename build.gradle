import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'application'
    id 'maven-publish'
    id 'signing'
    id 'net.nemerosa.versioning' version '3.0.0'
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

description = 'NightscoutConnector is a little library to connect to a Nightscout server and retrieve blood glucose values'

Date buildTimeAndDate = new Date()
ext {
    moduleName      = 'eu.hansolo.nightscoutconnector'
    buildDate       = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime       = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    ciOssrhUsername = System.getenv('OSSRH_USERNAME')
    ciOssrhPassword = System.getenv('OSSRH_PASSWORD')
    ciGHUser        = System.getenv('GH_USER')
    ciGHToken       = System.getenv('GH_TOKEN')
    gpgkey          = System.getenv("GPG_PRIVATE_KEY")
    gpgpassphrase   = System.getenv("PASSPHRASE")
}

application {
    mainModule = 'eu.hansolo.nightscoutconnector'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'eu.hansolo:toolbox:17.0.53'
    implementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
}


jar {
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect {  it.isDirectory() ? it : zipTree(it)  }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
        attributes(
                'Built-By'              : System.properties['user.name'],
                'Created-By'            : System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
                'Build-Date'            : project.buildDate,
                'Build-Time'            : project.buildTime,
                'Build-Revision'        : versioning.info.commit,
                'Specification-Title'   : project.name,
                'Specification-Version' : project.version,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.version,
                'Bundle-Name'           : project.name,
                'Bundle-License'        : 'https://www.apache.org/licenses/LICENSE-2.0;description=Apache License Version 2.0;link=https://spdx.org/licenses/Apache-2.0.html',
                'Bundle-Description'    : description,
                'Bundle-SymbolicName'   : 'eu.hansolo.nightscoutconnector',
                'Export-Package'        : 'eu.hansolo.nightscoutconnector',
                'Class-Path'            : configurations.runtimeClasspath.files.collect { it.getName() }.join(' ')
        )
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}


// create one jar for the javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

// create one jar for the source files
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    if (gpgkey && gpgpassphrase) {
        useInMemoryPgpKeys(gpgkey, gpgpassphrase)
    }

    sign configurations.archives
    sign publishing.publications
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            group      = 'eu.hansolo'
            artifactId = 'nightscoutconnector'
            version    = "${version}"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().dependencies.'*'.findAll {
                    it.groupId.text() == 'org.openjfx'
                }.each {
                    it.remove(it.classifier)
                }
            }

            pom {
                name        = 'NightscoutConnector'
                description = 'NightscoutConnector is a little library to connect to a Nightscout server and retrieve blood glucose values'
                url         = "https://github.com/HanSolo/nightscoutconnector/wiki"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url  = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id    = "HanSolo"
                        name  = "Gerrit Grunwald"
                        email = "han.solo@mac.com"
                    }
                }
                scm {
                    connection          = "scm:git:https://HanSolo/nightscoutconnector"
                    developerConnection = "scm:git:https://HanSolo/nightscoutconnector"
                    url                 = "https://github.com/HanSolo/nightscoutconnector"
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url  = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            try {
                credentials {
                    username = null == ciOssrhUsername ? ossrhUsername : ciOssrhUsername
                    password = null == ciOssrhPassword ? ossrhPassword : ciOssrhPassword
                }
            } catch (Exception e) {

            }
        }
        maven {
            name = "GitHubPackages"
            url  = "https://maven.pkg.github.com/HanSolo/nightscoutconnector"
            try {
                credentials {
                    username = null == ciGHUser  ? ghUser  : ciGHUser
                    password = null == ciGHToken ? ghToken : ciGHToken
                }
            } catch (Exception e) {

            }
        }
    }
}